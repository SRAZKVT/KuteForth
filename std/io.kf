include
	"syscalls.kf"
	"memory.kf"
	"numbers.kf"
end

func prints int ptr -- void end
func fprintc int int -- void end

func fprints
int // fd
int // size
ptr // buf
--
void
in rot write drop end

func fprintu 
int // fd
int // number
--
void
in
	if 32 alloc_temp_buffer do
		swap
		1 rot rot
		swap int 31 plus ptr dup 48 pwrite8 swap
		while dup 0 eq not do
			10 divmod swap 48 plus
			rot dup rot pwrite8
			ptrdec swap
			rot 1 plus rot rot
		end drop
		fprints
	else here prints "Allocation failed" prints 1 exit drop drop drop end
	32 free_temp_buffer
end

func fprinti
int // fd
int // number
--
void
in
	if dup 0 lt do
		neg
		over 45 fprintc
	end
	fprintu
end

func fprintc
int // fd
int // char
--
void
in
	if 1 alloc_temp_buffer do
		dup rot pwrite8
		1 swap fprints
	else here prints "Allocation failed" prints 1 exit drop drop drop end
	1 free_temp_buffer
end

func fprintb
int  // fd
bool // value
--
void
in
	if do
		"true" fprints
	else
		"false" fprints
	end
end

func prints int ptr -- void in 1 rot rot fprints end
func printb bool    -- void in 1 swap fprintb end
func printc int     -- void in 1 swap fprintc end
func printi int     -- void in 1 swap fprinti end
func printu int     -- void in 1 swap fprintu end

func get_file_size
int // fd
--
int // size
in
	if 144 alloc_temp_buffer not do
		here prints "Unable to allocate memory" prints
	end
	if dup rot fstat not do
		here prints "Unable to get file information\n" prints
		1 exit
	end
	48 ptrplus pread
end
