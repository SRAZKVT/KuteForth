include "std.kf" end

func read_file ptr -- void in
	0777 0 rot open
	if dup 0 lt do
		here eprints
		"Unable to open file\n" eprints
		1 exit
	end
	dup get_file_size
	while dup 0 gt do
		swap
		dup
		if 64000 alloc_temp_buffer do
			save 64000 swap rot read
			save rot prints
			rot rot minus
		else
			here eprints
			"Allocation failed\n" eprints
			1 exit drop drop end
		64000 free_temp_buffer
	end drop close
end

func read_stdin void -- void in
	if 2048 alloc_temp_buffer do
		while 2048 over 0 read dup 0 gt do
			over
			prints
		end drop drop
	else
		here eprints "Unable to allocate buffer\n" eprints
		1 exit drop
	end
	2048 free_temp_buffer
end

func main void -- void in
	0
	while
		if argc 1 gt do
			dup 1 plus argv
			cstr_to_str
			if dup pread8 '-' eq do
				ptrinc swap 1 minus swap
				while over 0 eq not do
					if dup pread8 'u' eq not do
						0 argv cstr_to_str eprints
						": invalid option -- '" eprints
						dup pread8 eprintc
						"'\n" eprints
						1 exit
					end
					ptrinc swap 1 minus swap
				end drop drop
				true
			else drop drop false end
		else false end
	do
		1 plus
	end // if arg is -u, ignore

	if argc over minus 1 eq do
		read_stdin 0 exit
	end
	1 plus
	while dup argc eq not do
		if dup argv pread8 45 eq do
			read_stdin
		else
			dup argv read_file
		end
		1 plus
	end drop
end
